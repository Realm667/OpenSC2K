class_name MultiplayerLaunchMenu
extends RefCounted

var owner_ref: WeakRef
var coop: ApplicationMultiplayer:
	get:
		return owner_ref.get_ref()
var role: OptionButton
var host_fields: VBoxContainer
var address_box: VBoxContainer
var password_label: Label
var action: Button
var shared_fields: VBoxContainer
var land_price: SpinBox
var goal_fields: VBoxContainer
var goal_picker: OptionButton
var goal_target: SpinBox

func _init(owner: ApplicationMultiplayer) -> void:
	owner_ref = weakref(owner)

func setup(body: VBoxContainer) -> void:
	MultiplayerText.setup()
	ApplicationMultiplayer.label(body, tr("LAN / direct IP · up to 8 players · matching game versions required."))
	coop.lobby = VBoxContainer.new()
	body.add_child(coop.lobby)
	role = OptionButton.new()
	role.add_item(tr("Host game"))
	role.add_item(tr("Join / Reconnect"))
	coop.lobby.add_child(role)
	var identity := HBoxContainer.new()
	coop.lobby.add_child(identity)
	var names := column(identity, 2)
	coop.player = ApplicationMultiplayer.field(names, tr("Player name"), "Mayor")
	var colors := column(identity, 1)
	ApplicationMultiplayer.label(colors, tr("Player colour"))
	var swatches := HBoxContainer.new()
	colors.add_child(swatches)
	var selection := ButtonGroup.new()
	coop.color_picker = ColorPickerButton.new()
	coop.color_picker.color = Color("46b4ff")
	coop.color_picker.edit_alpha = false
	coop.color_picker.text = "…"
	coop.color_picker.tooltip_text = tr("Custom colour…")
	coop.color_picker.custom_minimum_size = Vector2(34, 28)
	for value in ["46b4ff", "ff9c40", "66cf79", "e77ba8", "c7a0ff", "ffe173"]:
		var button := Button.new()
		button.toggle_mode = true
		button.button_group = selection
		button.custom_minimum_size = Vector2(22, 28)
		var image := Image.create(14, 14, false, Image.FORMAT_RGBA8)
		image.fill(Color(value))
		button.icon = ImageTexture.create_from_image(image)
		button.tooltip_text = "#" + value
		button.button_pressed = value == "46b4ff"
		swatches.add_child(button)
		button.pressed.connect(func() -> void:
			coop.color_picker.color = Color(value)
			coop.color_picker.color_changed.emit(Color(value)))
		coop.color_picker.color_changed.connect(func(color: Color) -> void:
			button.set_pressed_no_signal(color.to_html(false) == value))
	swatches.add_child(coop.color_picker)
	var picker := coop.color_picker.get_picker()
	picker.presets_visible = false
	picker.color_modes_visible = false
	var popup := coop.color_picker.get_popup()
	var color_body := VBoxContainer.new()
	popup.remove_child(picker)
	popup.add_child(color_body)
	color_body.add_child(picker)
	ApplicationMultiplayer.button(color_body, tr("Apply colour"), func() -> void:
		coop.color_picker.color = picker.color
		coop.color_picker.color_changed.emit(picker.color)
		popup.hide())
	host_fields = VBoxContainer.new()
	coop.lobby.add_child(host_fields)
	ApplicationMultiplayer.label(host_fields, tr("Game mode"))
	coop.mode_picker = OptionButton.new()
	for title in ["Co-op", "Competitive Shared", "Competitive Region"]:
		coop.mode_picker.add_item(tr(title))
	host_fields.add_child(coop.mode_picker)
	ApplicationMultiplayer.label(host_fields, tr("City"))
	coop.city_picker = OptionButton.new()
	coop.city_picker.add_item(tr("New city"))
	coop.city_picker.add_item(tr("Saved city"))
	host_fields.add_child(coop.city_picker)
	coop.use_current = CheckBox.new()
	coop.use_current.text = tr("Create a copy")
	coop.use_current.tooltip_text = tr("Keep the selected save unchanged and save this game separately.")
	coop.use_current.button_pressed = true
	host_fields.add_child(coop.use_current)
	shared_fields = VBoxContainer.new()
	host_fields.add_child(shared_fields)
	ApplicationMultiplayer.label(shared_fields, tr("Neutral land price per tile"))
	land_price = SpinBox.new()
	land_price.min_value = 0
	land_price.max_value = 1000
	land_price.value = 5
	land_price.prefix = "$"
	shared_fields.add_child(land_price)
	land_price.tooltip_text = tr("Price for additional neutral land. At the shared start, every city receives an equal free starting area; its treasury stays intact. Saved games retain their rules.")
	goal_fields = VBoxContainer.new()
	host_fields.add_child(goal_fields)
	ApplicationMultiplayer.label(goal_fields, tr("Victory goal"))
	goal_picker = OptionButton.new()
	for title in ["Endless game", "Population", "Wealth (funds minus debt)"]:
		goal_picker.add_item(tr(title))
	goal_fields.add_child(goal_picker)
	goal_target = SpinBox.new()
	goal_target.min_value = 1
	goal_target.max_value = 2147483647
	goal_target.value = 10000
	goal_fields.add_child(goal_target)
	goal_picker.item_selected.connect(func(index: int) -> void:
		goal_target.value = 100000 if index == 2 else 10000
		update())
	var network := HBoxContainer.new()
	coop.lobby.add_child(network)
	address_box = column(network, 2)
	coop.address = ApplicationMultiplayer.field(address_box, tr("Host address"), "127.0.0.1")
	var ports := column(network, 1)
	ApplicationMultiplayer.label(ports, tr("TCP port"))
	coop.port = SpinBox.new()
	coop.port.min_value = 1024
	coop.port.max_value = 65535
	coop.port.value = 20000
	ports.add_child(coop.port)
	password_label = ApplicationMultiplayer.label(coop.lobby, "")
	coop.join_code = LineEdit.new()
	coop.join_code.secret = true
	coop.lobby.add_child(coop.join_code)
	action = ApplicationMultiplayer.button(coop.lobby, "", func() -> void:
		if role.selected == 0:
			coop.start_host()
		else:
			coop.start_join())
	role.item_selected.connect(func(_index: int) -> void: update())
	coop.mode_picker.item_selected.connect(func(_index: int) -> void: update())
	coop.city_picker.item_selected.connect(func(_index: int) -> void:
		coop.selected_city_path = ""
		update())
	role.tooltip_text = tr("Host a game on this computer, or connect to an existing host.")
	coop.player.tooltip_text = tr("Your name in the lobby, chat and scoreboard.")
	coop.color_picker.tooltip_text = tr("Choose a custom colour for your cursor, territory and statistics, then Apply colour.")
	coop.address.tooltip_text = tr("Enter the host computer's LAN address or reachable IP address.")
	coop.port.tooltip_text = tr("The host and joining players must use the same TCP port.")
	coop.mode_picker.tooltip_text = tr("Co-op: one shared city. Shared: separate cities on one map. Region: independent neighbouring city maps.")
	coop.city_picker.tooltip_text = tr("Generate new terrain, or continue a saved multiplayer game. Saved games keep their rules and player seats.")
	update()

func update() -> void:
	var hosting := role.selected == 0
	host_fields.visible = hosting
	address_box.visible = not hosting
	password_label.text = tr("Password (optional)") if hosting else tr("Password")
	action.text = tr("Create game") if hosting else tr("Join / Reconnect")
	coop.use_current.visible = coop.city_picker.selected == 1
	shared_fields.visible = coop.mode_picker.selected == 1 and coop.city_picker.selected == 0
	goal_fields.visible = coop.mode_picker.selected != 0 and coop.city_picker.selected == 0
	goal_target.visible = goal_picker.selected != 0
	goal_picker.tooltip_text = goal_help()
	goal_target.tooltip_text = goal_help()
	coop.join_code.tooltip_text = tr("Leave empty to allow joining without a password.") if hosting else tr("Enter the game's password if the host set one; otherwise leave empty.")
	action.tooltip_text = tr("Configure the city, then wait in the lobby until everyone is ready.") if hosting else tr("Connect and select a new or reserved player seat. The host controls the game rules.")
	fit.call_deferred()

func fit() -> void:
	if not is_instance_valid(coop.panel):
		return
	var available := coop.app.get_viewport().get_visible_rect().size
	var height := int(coop.lobby.get_parent().get_combined_minimum_size().y) + 28
	coop.panel.size = Vector2i(mini(700, int(available.x) - 40), mini(height, int(available.y) - 70))

static func column(parent: Control, ratio: float) -> VBoxContainer:
	var box := VBoxContainer.new()
	box.size_flags_horizontal = Control.SIZE_EXPAND_FILL
	box.size_flags_stretch_ratio = ratio
	parent.add_child(box)
	return box

func goal_help() -> String:
	var hint := tr("Population: residents of your own city. Suggested targets: 2,000 for a short development goal, 10,000 for a medium goal, 50,000 for a long goal.") if goal_picker.selected == 1 else tr("Net wealth: treasury minus bank bonds and outstanding player debt. Suggested targets: $50,000, $100,000 or $500,000 for increasingly long economic goals.")
	if goal_picker.selected == 0:
		return tr("Build without a victory target or time limit.")
	return hint + "\n" + tr("The target must then be held for one full game year (300 game days). At uninterrupted simulation speed, that year takes about 4 minutes at Turtle, 2 at Llama or 1 at Cheetah. Swallow advances one day per processed frame (about 5 seconds per year at 60 simulation frames/s). Development time is additional and depends on difficulty, terrain, experience, pauses and computer performance; these are not match-duration guarantees.")
